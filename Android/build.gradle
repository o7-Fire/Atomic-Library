apply plugin: 'java-library'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation aproj(":Atomic")
}
ext{
    sdkVersion = '30'
    sdkRoot = System.getenv("ANDROID_HOME")
    doExec = { cmd ->
        def proc = cmd.execute(null, new File("$buildDir/libs"))
        proc.waitForProcessOutput(System.out, System.err)
    }
}

jar{
    dependsOn ":Atomic:jar"
    archiveFileName = "${project.archivesBaseName}.jar"
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

task jarAndroid{
    dependsOn "jar"

    doLast{
        //collect dependencies needed for desugaring
        def files = (configurations.compileClasspath.asList() + configurations.runtimeClasspath.asList() + [new File("$sdkRoot/platforms/android-$sdkVersion/android.jar")])
        def dependencies = files.collect{ "--classpath $it.path" }.join(" ")
        println "runtimeClasspath:"
        for(String e : configurations.runtimeClasspath.asList())
            println e

        //dex and desugar files - this requires d8 in your PATH
        doExec("d8 $dependencies --min-api 14 ${project.archivesBaseName}.jar")
    }
}
sourceCompatibility = "1.7"
targetCompatibility = "1.7"